; Script generated by the Inno Setup Script Wizard.

#define MyAppName "Lighting Controls Headend"
#define MyAppVersion "1.1.6"
#define MyAppPublisher "Lighting Controls ltd"
#define MyAppURL "http://lightingcontrols.ltd.uk/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{F2281668-27EC-4DFC-8AD6-D8D6EE868243}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\xampp\htdocs\lcheadend
DisableDirPage=yes
DefaultGroupName={#MyAppName}
AllowNoIcons=yes
LicenseFile=I:\Projectos\LightCtrl\trunk\installer\files\license.txt
InfoBeforeFile=I:\Projectos\LightCtrl\trunk\installer\files\release_notes.txt
InfoAfterFile=I:\Projectos\LightCtrl\trunk\installer\files\info_after.txt
OutputDir=I:\Projectos\LightCtrl\trunk\installer
OutputBaseFilename=LC_Headend_Setup
SetupIconFile=I:\Projectos\LightCtrl\trunk\installer\www\themes\intsys\images\lightctr.ico
Compression=lzma
SolidCompression=yes
UninstallDisplayName=LC_Headend_Uninstall

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[CustomMessages]
FrontendLink=%1 Direct Link

[Types]
Name: "full"; Description: "Full installation"
Name: "custom"; Description: "Custom installation"; Flags: iscustom

[Files]
Source: "I:\Projectos\LightCtrl\trunk\installer\files\release_notes.txt"; DestDir: "{app}"; Flags: ignoreversion; 

Source: "I:\Projectos\LightCtrl\trunk\installer\www\cli.php"; DestDir: "{app}"; Flags: ignoreversion; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\.htaccess"; DestDir: "{app}"; Flags: ignoreversion; 

Source: "I:\Projectos\LightCtrl\trunk\installer\www\css\*"; DestDir: "{app}\css"; Flags: ignoreversion recursesubdirs createallsubdirs; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\framework\*"; DestDir: "{app}\framework"; Flags: ignoreversion recursesubdirs createallsubdirs; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\images\*"; DestDir: "{app}\images"; Flags: ignoreversion recursesubdirs createallsubdirs skipifsourcedoesntexist; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\js\*"; DestDir: "{app}\js"; Flags: ignoreversion recursesubdirs createallsubdirs; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\mpdf\*"; DestDir: "{app}\mpdf"; Flags: ignoreversion recursesubdirs createallsubdirs; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\commands\*"; DestDir: "{app}\protected\commands"; Flags: ignoreversion recursesubdirs createallsubdirs; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\components\*"; DestDir: "{app}\protected\components"; Flags: ignoreversion recursesubdirs createallsubdirs; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\controllers\*"; DestDir: "{app}\protected\controllers"; Flags: ignoreversion recursesubdirs createallsubdirs; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\migrations\*"; DestDir: "{app}\protected\migrations"; Flags: ignoreversion recursesubdirs createallsubdirs skipifsourcedoesntexist; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\models\*"; DestDir: "{app}\protected\models"; Flags: ignoreversion recursesubdirs createallsubdirs; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\transfers\*"; DestDir: "{app}\protected\transfers"; Flags: ignoreversion recursesubdirs createallsubdirs; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\views\*"; DestDir: "{app}\protected\views"; Flags: ignoreversion recursesubdirs createallsubdirs; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\.htaccess"; DestDir: "{app}\protected"; Flags: ignoreversion; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\yiic"; DestDir: "{app}\protected"; Flags: ignoreversion; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\yiic.bat"; DestDir: "{app}\protected\"; Flags: ignoreversion; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\protected\yiic.php"; DestDir: "{app}\protected\"; Flags: ignoreversion; 
Source: "I:\Projectos\LightCtrl\trunk\installer\www\themes\*"; DestDir: "{app}\themes"; Flags: ignoreversion recursesubdirs createallsubdirs; 

; Prepared config files
Source: "I:\Projectos\LightCtrl\trunk\installer\files\configs\main.php"; DestDir: "{app}\protected\config\temp"; Flags: ignoreversion; 
Source: "I:\Projectos\LightCtrl\trunk\installer\files\configs\console.php"; DestDir: "{app}\protected\config\temp"; Flags: ignoreversion;

; Prepared Index file
Source: "I:\Projectos\LightCtrl\trunk\installer\files\index.php"; DestDir: "{app}"; Flags: ignoreversion; 

; Config Merger Tool
Source: "I:\Projectos\LightCtrl\trunk\installer\files\config_merger.bat"; DestDir: "{app}\setup"; Flags: deleteafterinstall; 

; Install Backup Tool
Source: "I:\Projectos\LightCtrl\trunk\installer\BackupTool\*"; DestDir: "{app}\BackupTool"; Flags: ignoreversion recursesubdirs createallsubdirs; 

; DB Script and Batch installer/updater
Source: "I:\Projectos\LightCtrl\trunk\installer\files\db_installer.bat"; DestDir: "{app}\setup"; Flags: deleteafterinstall; 
Source: "I:\Projectos\LightCtrl\trunk\installer\files\lcheadend_v1.0.0.sql"; DestDir: "{app}\setup"; Flags: deleteafterinstall; 

; CronJobs 
Source: "I:\Projectos\LightCtrl\trunk\installer\files\cronjobs_create.bat"; DestDir: "{app}\setup"; Flags: deleteafterinstall; 
Source: "I:\Projectos\LightCtrl\trunk\installer\files\cronjobs_delete.bat"; DestDir: "{app}\setup"; Flags: deleteafterinstall; 

; Configs in mysql.ini and php.ini
Source: "I:\Projectos\LightCtrl\trunk\installer\files\configs\my.ini"; DestDir: "C:\xampp\mysql\bin"; 
Source: "I:\Projectos\LightCtrl\trunk\installer\files\configs\php.ini"; DestDir: "C:\xampp\php"; 

[InstallDelete] 
; Delete existing Assets folder files
Type: filesandordirs; Name: "{app}\assets"
; Delete existing Models and Componentes folder files (to avoid colision with new structure)
Type: filesandordirs; Name: "{app}\protected\models\*.*"
Type: filesandordirs; Name: "{app}\protected\components\*.*"

[UninstallDelete] 
; Delete existing Assets folder files
Type: filesandordirs; Name: "{app}\data\*.*"
Type: filesandordirs; Name: "{app}\assets\*.*"
Type: filesandordirs; Name: "{app}\setup\*.*"
Type: filesandordirs; Name: "{app}\protected\runtime\*.*"

[Dirs]
Name: "{app}\assets"
Name: "{app}\data"
Name: "{app}\data\configs"
Name: "{app}\data\plans"
Name: "{app}\protected\runtime"

[Icons]
Name: "{commondesktop}\{cm:FrontendLink,Headend}"; Filename: "C:\Program Files\Internet Explorer\iexplore.exe"; Parameters: """http://localhost/lcheadend"""; IconFilename: "{uninstallexe}";
Name: "{group}\{cm:FrontendLink,{#MyAppName}}"; Filename: "C:\Program Files\Internet Explorer\iexplore.exe"; Parameters: """http://localhost/lcheadend"""; IconFilename: "{uninstallexe}";
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Run]
; Run Config Merger
Filename: "{app}\setup\config_merger.bat"; StatusMsg: "Merging Application Configs..."; Flags: runhidden waituntilterminated

; Run DB Installer / Updater
Filename: "{app}\setup\db_installer.bat"; StatusMsg: "Verifying Database instance..."; Flags: runhidden waituntilterminated

; Run CronJobs
Filename: "{app}\setup\cronjobs_create.bat"; StatusMsg: "Applying CronJobs..."; Flags: runhidden waituntilterminated

[UninstallRun]
Filename: "{app}\setup\cronjobs_delete.bat"; StatusMsg: "Deleting old CronJobs..."; Flags: runhidden waituntilterminated

[Code]
function XamppIsNotInstalled: Boolean;
begin
    Result := (not RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\Wow6432NodeMicrosoft\xampp') and not RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\xampp'));
end;

function InitializeSetup() : Boolean;
var
   ResultCode: Integer;
begin
  if XamppIsNotInstalled() then
  begin
    // Inform User that xampp is not installed and confirm if instalation should continue
    if MsgBox('XAMPP does not appear to be installed and it is required to install/run this application.'#13#13 + 'Do you want to continue install anyway?', mbConfirmation, MB_YESNO or MB_DEFBUTTON2) = IDNO then
    begin
      Abort;
    end;
  end;

  // Prompt User for removing existing cronjobs
  if MsgBox('Do you want setup to remove any existing Cronjobs?'#13#13 + 'Warning: If you select Yes and quit the installer, they will not be re-created', mbConfirmation, MB_YESNO or MB_DEFBUTTON2) = IDYES then
  begin
    ExtractTemporaryFile('cronjobs_delete.bat');
    Exec(ExpandConstant('{tmp}\cronjobs_delete.bat'), '', '', SW_SHOW, ewWaitUntilTerminated, ResultCode)
  end;  

  Result := true;
end;